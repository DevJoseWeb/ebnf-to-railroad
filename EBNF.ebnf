(* This is a modifed version of the informal definition in the ISO 14977 *)
syntax = syntax-rule, {syntax-rule};

syntax-rule = meta-identifier, '=', definitions-list, ';';

definitions-list = single-definition , { '|', single-definition };

single-definition = term, { ',', term };

term = factor, [ '-', terminal ];

factor = [integer, '*'], primary;

primary = terminal | non-terminal;

non-terminal 
    = optional-sequence
    | repeated-sequence
    | grouped-sequence
    | meta-identifier
    ;

terminal
    = special-sequence
    | terminal-string
    | empty
    ;

empty = ;

optional-sequence = '[', definitions-list, ']';

repeated-sequence = '{', definitions-list, '}';

grouped-sequence = '(', definitions-list, ')';

terminal-string = "'", character - "'", {character - "'"}, "'"
                | '"', character - '"', {character - '"'}, '"'
                ;

meta-identifier = letter, {letter | decimal-digit};

integer = decimal-digit, {decimal-digit};

special-sequence = '?', {character - '?'}, '?';

comment = '(*', {comment-symbol}, '*)';

comment-symbol = comment
               | terminal-string
               | special-sequence
               | character
               ;